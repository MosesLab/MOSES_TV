;+
;NAME:
;  MTV
;PURPOSE:
;  Display images from a MOSES exposure, with all 3 orders on the same screen, 
;  some image statistics, and useful info from the log file.
;CALLING SEQUENCE:
;  mtv, index, number, byteorder=byteorder
;INPUTS:
;  index --- exposure index structure from mxml().
;  number --- desired image number from the index.
;OPTIONAL KEYWORD INPUTS:
;  directory --- string to prepend to filename.
;  histeq --- hist_equal the display instead of tvscl.
;  sqrt --- square root scale
;MODIFICATION HISTORY:
;  Written by CCK, Summer 2005
;  2005-Nov-30 CCK added directory keyword.
;-
pro mtv, index, number, byteorder=byteorder, directory=directory, $
   histeq=histeq, gamma=gamma

!p.charsize=2
size = index.numpixels[number]


moses_read, index.filename[number], minus, zero, plus, noise, $
   size=size, index.channels[number, *], byteorder=byteorder, error=error, directory=directory

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;added the following for debug:
gamma=1

;Make the big window, and put it in a good place.
window, 13, xpos=10, ypos=30, xsize=2048, ysize=1280, $
   title='<MTV>------- M O S E S -------<MTV>'

if error eq 0 then begin
   if keyword_set(histeq) then begin
      ;Resize and scale images for display
      zd = rebin(hist_equal(zero), 2048, 1024)
      md = rebin(hist_equal(minus), 512, 256)
      pd = rebin(hist_equal(plus),  512, 256)
      nd = rebin(bytscl(noise), 512, 256) ;noise is always bytscl'd.  
   endif else begin
      ;Resize and scale images for display
      zd = rebin(bytscl(zero^gamma), 2048, 1024)
      md = rebin(bytscl(minus^gamma), 512, 256)
      pd = rebin(bytscl(plus^gamma),  512, 256)
      nd = rebin(bytscl(noise^gamma), 512, 256)
   endelse
   
   ;Display images
   tv, zd, 0,256
   tv, md, 512,0
   tv, pd, 1024,0
   tv, nd, 1536,0

   ;Create & display histogram
   hist_z = histogram(zero,  Nbins=512, min=0, max=16383)
   hist_m = histogram(minus, Nbins=512, min=0, max=16383)
   hist_p = histogram(plus,  Nbins=512, min=0, max=16383)
   kcounts = findgen(512) * (16384/512)/1000

   plot, kcounts, hist_m, /noerase, linestyle=1, /ylog, $
      /device, position = [20,20, 511,255], charsize=1, $
      xrange=[0,16384]/1000, yrange=[1,max(hist_m)], /xstyle
   oplot, kcounts, hist_p, linestyle=2
   oplot, kcounts, hist_z


   ;Saturated pixel count:
   ss = where(zero ge 16383)
   if (ss[0] eq -1) then sat=0 else sat = n_elements(ss)
   sat_z = strcompress('#sat: '+string(sat))

   ss = where(minus ge 16383)
   if (ss[0] eq -1) then sat=0 else sat = n_elements(ss)
   sat_m = strcompress('#sat: '+string(sat))

   ss = where(plus ge 16383)
   if (ss[0] eq -1) then sat=0 else sat = n_elements(ss)
   sat_p = strcompress('#sat: '+string(sat))

   ss = where(noise ge 16383)
   if (ss[0] eq -1) then sat=0 else sat = n_elements(ss)
   sat_n = strcompress('#sat: '+string(sat))


   ;Display annotations
   left = 0.0
   right = 1.0
   center = 0.5

   ;define text color (to be visible against all grayscale backgrounds)
   red1=[255,255,255]

   ;Saturated pixel count in LL corner of each image
   xyouts, 2,270, sat_z, align=left, charsize=3.0, charthick=2.0, color=red1, /device
   xyouts, 514,2, sat_m, align=left, charsize=3.0, charthick=2.0, color=red1, /device
   xyouts, 1028,2, sat_p, align=left, charsize=3.0, charthick=2.0, color=red1, /device
   xyouts, 1544,2, sat_n, align=left, charsize=3.0, charthick=2.0, color=red1, /device

   ;Channel label, LR corner of each image
   xyouts, 2042,270, "m = 0",  align=right, charsize=3.0, charthick=2.0, color=red1, /device
   xyouts, 1018,2,    "m = -1", align=right, charsize=3.0, charthick=2.0, color=red1, /device
   xyouts, 1530,2,    "m = +1", align=right, charsize=3.0, charthick=2.0, color=red1, /device
   xyouts, 2042,2,   "noise",  align=right, charsize=3.0, charthick=2.0, color=red1, /device

   ;Date, time, filename, exp time at top
   xyouts, 2,1250, index.date[number]+" "+index.time[number], $
      align=left, charsize=3.0, charthick=2.0, color=red1, /device
   xyouts, 1024,1250, index.filename[number], align=center, charsize=3.0, charthick=2.0, color=red1, /device
   duration_string = strcompress("exp time: "+string(index.exptime[number])+" us")
   xyouts, 2042,1250, duration_string, align=right, charsize=3.0, charthick=2.0, color=red1, /device

endif else begin
   ;Display file not found error
   mesg = "IMAGE "+string(number)+" NOT FOUND: "+index.filename[number]
   if n_elements(directory) ne 0 then mesg=mesg+" in directory "+directory
   xyouts,10,10,mesg, charthick=1.5, color='79', /device
endelse

end
